//--------------------------------------------------------------------------------------------------
//= init_h.hsp
//--------------------------------------------------------------------------------------------------
//	ウィンドウ / バッファ / コンパイルオプションなどの初期化
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= 終了処理ラベルを設定
	//----------------------------------------------------------------------------------------------
		onexit goto *__end

	//----------------------------------------------------------------------------------------------
	//= 画像バッファ初期化
	//----------------------------------------------------------------------------------------------

		//------------------------------------------------------------------------------------------
		//= マップチップ(下層)
		//------------------------------------------------------------------------------------------
			celload "data/image/mapchip_lower_layer.png", SCR_MAPCHIP_LOWER
			celdiv SCR_MAPCHIP_LOWER, TILE_SIZE_WH, TILE_SIZE_WH

		//------------------------------------------------------------------------------------------
		//= マップチップ(上層)
		//------------------------------------------------------------------------------------------
			celload "data/image/mapchip_upper_layer.png", SCR_MAPCHIP_UPPER
			celdiv SCR_MAPCHIP_UPPER, TILE_SIZE_WH+2, TILE_SIZE_WH+2, 1, 1

		//------------------------------------------------------------------------------------------
		//= 下層マップコピー元バッファ
		//------------------------------------------------------------------------------------------
			buffer SCR_MAPBUF_LOWER, TILE_SIZE_WH*MAP_SIZE_WH, TILE_SIZE_WH*MAP_SIZE_WH
			repeat MAP_SIZE_WH
				x = cnt
				repeat MAP_SIZE_WH
					y = cnt
					pos x*TILE_SIZE_WH, y*TILE_SIZE_WH
					color ALPHA_COLOR: gmode 4, , , 256: celput SCR_MAPCHIP_LOWER, MapDataLower(x, y)
				loop
			loop

		//------------------------------------------------------------------------------------------
		//= キャラクター画像
		//------------------------------------------------------------------------------------------
			repeat CHARACTER_CLASS_MAX
				path = "data/image/character_"+CharacterClassName(cnt)+".png"
				id = SCR_CHARACTER+cnt
				celload path, id
				celdiv SCR_CHARACTER+cnt, CHARACTER_SIZE_WH, CHARACTER_SIZE_WH, CHARACTER_SIZE_WH/2, CHARACTER_SIZE_WH/2
			loop

		//------------------------------------------------------------------------------------------
		//= システムアイコン
		//------------------------------------------------------------------------------------------
			celload "data/image/system_icon.png", SCR_SYSTEM_ICON
			celdiv SCR_SYSTEM_ICON, SYSTEM_ICON_SIZE_WH, SYSTEM_ICON_SIZE_WH, SYSTEM_ICON_SIZE_WH/2, SYSTEM_ICON_SIZE_WH/2

		//------------------------------------------------------------------------------------------
		//= ボタンアイコン
		//------------------------------------------------------------------------------------------
			celload "data/image/button_icon.png", SCR_BUTTON_ICON
			celdiv SCR_BUTTON_ICON, BUTTON_ICON_SIZE_WH, BUTTON_ICON_SIZE_WH, BUTTON_ICON_SIZE_WH/2, BUTTON_ICON_SIZE_WH/2

	//----------------------------------------------------------------------------------------------
	//= メインウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		screen SCR_MAIN, WINDOW_SIZE_W, WINDOW_SIZE_H

		//アイテムセレクトボタンを追加(一時的)
		bg=MENU_LIGHTCOLOR : bgh=BUTTON_BGCOLOR_HOVER : tx=0,0,0 : txh=0,0,0
		mgButtonColor bg, bgh, tx, txh
		size = 36 : margin = 4
		xsize = size*5+margin*4
		x = MENU_POS_X+(MENU_SIZE_W/2)-(xsize/2) : y = MENU_POS_Y+75
		k = 0
		repeat 3
			i = cnt
			repeat 5
				j = cnt
				mgButton "", *__ItemSelectButton, x+(size+margin)*j, y+(size+margin)*i, size, size, 1, SCR_BUTTON_ICON, k
				ItemSelectButtonId(k) = stat
				k++
			loop
		loop

		//リセットボタンを追加(一時的)
		pos MENU_POS_X+MENU_SIZE_W/2-64/2, ginfo_winy-64
		bg=BUTTON_BGCOLOR : bgh=BUTTON_BGCOLOR_HOVER : tx=BUTTON_TEXTCOLOR : txh=BUTTON_TEXTCOLOR
		mgButtonColor bg, bgh, tx, txh
		mgButton "Reset", *__MapLowerReset, , , 64, 27, 1