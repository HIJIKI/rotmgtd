#module _GetKeyInput_
	//----------------------------------------------------------------------------------------------
	//= モジュール初期化命令(インクルード時に自動実行)
	//----------------------------------------------------------------------------------------------
	#deffunc GetKeyInput_init
		dim inKeyCnt,		1	//扱うキーの総数
		dim inKeyCode,		1	//扱うキーのキーコード
		dim inKeyBuf,		1	//キーの状態バッファ(inKeyCodeに対応)
		dim inKeyStat,		1	//キーの状態(押されている間足され続ける, そうでなければ0)
	return
	//----------------------------------------------------------------------------------------------
	//= 使用するキーを登録する命令
	//----------------------------------------------------------------------------------------------
	// AddKeyInput p1
	// p1 = キーコード(getkeyと同じ)
	//----------------------------------------------------------------------------------------------
	#deffunc AddKeyInput int _kc	;keycode
		inKeyCode(inKeyCnt) = _kc
		inKeyCnt++
	return
	//----------------------------------------------------------------------------------------------
	//= 登録したキーの状態をチェックする命令
	//----------------------------------------------------------------------------------------------
	// この命令は1ループに1回必ず呼び出すこと。
	//----------------------------------------------------------------------------------------------
	#deffunc CheckKeyInput
		repeat inKeyCnt
			getkey inKeyBuf(cnt), inKeyCode(cnt)
			if(inKeyBuf(cnt) == 1) {
				inKeyStat(cnt)++
			} else {
				inKeyStat(cnt) = 0
			}
		loop
	return
	//----------------------------------------------------------------------------------------------
	//= [int]指定したキーの状態を返す関数
	//----------------------------------------------------------------------------------------------
	#defcfunc GetKeyInput int _idx
		ret = inKeyStat(_idx)
	return ret
#global
GetKeyInput_init

//--------------------------------------------------------------------------------------------------
//= 以下サンプル
//--------------------------------------------------------------------------------------------------
#if 0
	//使うキーの定数をenumで定義
	#enum KB_Z	=0
	#enum KB_X
	#enum KB_UP
	#enum KB_DOWN
	#enum KB_LEFT
	#enum KB_RIGHT
	#enum KEY_MAX
	//必ず定義した順番でAddすること
	AddKeyInput 'Z'
	AddKeyInput 'X'
	AddKeyInput 38	//↑キー(getkeyと同じ)
	AddKeyInput 40	//↓
	AddKeyInput 37	//←
	AddKeyInput 39	//→
	*__main
		CheckKeyInput
		color 255,255,255:boxf:color:pos 16, 16
		repeat KEY_MAX
			mes "キー"+cnt+"の状態:"+GetKeyInput(cnt)
		loop
		mes "登録キー数:"+KEY_MAX
		wait 1
	goto *__main
#endif