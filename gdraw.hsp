//--------------------------------------------------------------------------------------------------
//= gdraw.hsp
//--------------------------------------------------------------------------------------------------
//	画面を 初期化 / 描画 / 更新
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= 画面初期化, 画面の更新を停止
	//----------------------------------------------------------------------------------------------
		redraw 0: color: boxf

	//----------------------------------------------------------------------------------------------
	//= マップの描画
	//----------------------------------------------------------------------------------------------
		timer_start timer_cnt(), "地形描画"
		//下層(バッファから一括コピー)
		gmode 0, TILE_SIZE_WH*MAP_SIZE_WH, TILE_SIZE_WH*MAP_SIZE_WH
		pos 0, 0
		gcopy SCR_MAPBUF_LOWER, 0, 0

		//上層
		color ALPHA_COLOR
		gmode 4, TILE_SIZE_WH, TILE_SIZE_WH, 256
		repeat MAP_SIZE_WH
			x = cnt
			repeat MAP_SIZE_WH
				y = cnt
				if(MapDataUpper(x, y) != 0){
					pos TILE_SIZE_WH*x, TILE_SIZE_WH*y
					celput SCR_MAPCHIP_UPPER, MapDataUpper(x, y)
				}
			loop
		loop
		timer_end timer_cnt()

	//----------------------------------------------------------------------------------------------
	//= 敵キャラクターの描画
	//----------------------------------------------------------------------------------------------
		timer_start timer_cnt(), "敵キャラ描画"
		sysfont:color 255
		repeat ENEMY_MAX
			if(EnemyFlag(cnt) == TRUE){
				pos EnemyPosX(cnt)-7, EnemyPosY(cnt)-8
				mes "●"
			}
		loop
		timer_end timer_cnt()

	//----------------------------------------------------------------------------------------------
	//= カーソルを合わせているマスのグリッドを描画
	//----------------------------------------------------------------------------------------------
		if(CursorPosX != -1){
			x = CursorPosX*TILE_SIZE_WH
			y = CursorPosY*TILE_SIZE_WH
			color 255, 255, 255
			boxl x, y, x+TILE_SIZE_WH-1, y+TILE_SIZE_WH-1
		}

	//----------------------------------------------------------------------------------------------
	//= メニューボード描画
	//----------------------------------------------------------------------------------------------
		timer_start timer_cnt(), "メニューボード描画"
		color MENU_BGCOLOR
		boxf MENU_POS_X, MENU_POS_Y, MENU_POS_X+MENU_SIZE_W, MENU_POS_Y+MENU_SIZE_H

		//------------------------------------------------------------------------------------------
		//= 所持金の描画
		//------------------------------------------------------------------------------------------
			//枠
			color MENU_LIGHTCOLOR
			ChamferFill SYSTEM_MONEY_POS_X, SYSTEM_MONEY_POS_Y, SYSTEM_MONEY_SIZE_W, SYSTEM_MONEY_SIZE_H
			//アイコン
			pos SYSTEM_MONEY_POS_X+8, SYSTEM_MONEY_POS_Y+1
			color ALPHA_COLOR
			gmode 4, , , 256
			celput SCR_SYSTEM_ICON, 0
			//文字
			color MENU_TEXTCOLOR
			font "Arial", 16, 1
			hjkMes "99999999", SYSTEM_MONEY_POS_X+SYSTEM_MONEY_SIZE_W-16, SYSTEM_MONEY_POS_Y+SYSTEM_MONEY_SIZE_H/2, 2, 1

		//------------------------------------------------------------------------------------------
		//= ボタンの描画
		//------------------------------------------------------------------------------------------
			mgButtonDraw

		timer_end timer_cnt()

	//----------------------------------------------------------------------------------------------
	//= 再描画(デバッグ中はスキップ)
	//----------------------------------------------------------------------------------------------
		if( DEBUG_FLAG == FALSE ){
			redraw 1
		}